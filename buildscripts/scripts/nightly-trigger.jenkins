BRANCH_NAME = scm.branches[0].name
ROOT_FOLDER = currentBuild.fullProjectName.split('/')[0]
FOLDER = Jenkins.instance.getItemByFullName(currentBuild.fullProjectName).getParent().getFullName()

def DISTRO_LIST_DEFAULT = ''
def NODE = ''
def EDITION_DEFAULT = ''
def DEPLOY_TO_WEBSITE = false
def JOB_EDITION = JOB_BASE_NAME.split("-")[-1]
def ENABLE_NIGHTLY_TRIGGER = false

withFolderProperties{
    switch (JOB_EDITION) {
        case 'cee':
            DISTRO_LIST_DEFAULT = env.DISTRO_LIST
            EDITION_DEFAULT = 'enterprise'
            CRON_PATTERN = '0 0 * * *' // 0:00 am
            RUN_INTEGRATION_TESTS = true
            RUN_IMAGE_TESTS = true
            DEPLOY_TO_WEBSITE = !ROOT_FOLDER.startsWith('Testing') // Do not deploy from Testing folder.
            BUILD_IMAGE = true
            break
        case 'cre':
            DISTRO_LIST_DEFAULT = env.DISTRO_LIST_CRE
            EDITION_DEFAULT = 'raw'
            CRON_PATTERN = '0 2 * * *' // 2:00 am
            RUN_INTEGRATION_TESTS = true
            RUN_IMAGE_TESTS = true
            DEPLOY_TO_WEBSITE = false
            BUILD_IMAGE = true
            break
        case 'cfe':
            DISTRO_LIST_DEFAULT = env.DISTRO_LIST_CFE
            EDITION_DEFAULT = 'free'
            CRON_PATTERN = '30 2 * * *' // 2:30 am
            // CFE is basically a CEE, so we do not need to test it
            RUN_INTEGRATION_TESTS = false
            RUN_IMAGE_TESTS = false
            DEPLOY_TO_WEBSITE = false
            BUILD_IMAGE = false
            break
        case 'cpe':
            DISTRO_LIST_DEFAULT = env.DISTRO_LIST_CPE
            EDITION_DEFAULT = 'plus'
            CRON_PATTERN = '0 3 * * *' // 3:00 am
            // Enable this later when we have CPE specific integration tests.
            // Until then it's similar to the CEE.
            RUN_INTEGRATION_TESTS = false
            RUN_IMAGE_TESTS = true
            // Do not publish anything. We only
            // use the packages for our own tests - for now.
            DEPLOY_TO_WEBSITE = false
            BUILD_IMAGE = true
            break
        default:
            throw new Exception('unknown trigger job')
    }
    NODE = env.BUILD_NODE
    ENABLE_NIGHTLY_TRIGGER = env.ENABLE_NIGHTLY_TRIGGER == "True"
}

properties([
    buildDiscarder(logRotator(artifactDaysToKeepStr: '', artifactNumToKeepStr: '', daysToKeepStr: '7', numToKeepStr: '14')),
    parameters([
        string(name: 'DISTROS', defaultValue: DISTRO_LIST_DEFAULT, description: 'List of targeted distros' ),
        string(name: 'EDITION', defaultValue: EDITION_DEFAULT, description: 'Edition: raw, enterprise, managed or plus' )
    ]),
    pipelineTriggers(
        ENABLE_NIGHTLY_TRIGGER ? [cron("${CRON_PATTERN}")] : []
    )
])

node: {
    label 'NODE'
    stage('Build Packages') {
        build(job: "${FOLDER}/nightly-${JOB_EDITION}/build-cmk-packages",
              parameters: [
                [$class: 'StringParameterValue', name: 'DISTROS', value: DISTROS],
                [$class: 'StringParameterValue', name: 'EDITION', value: EDITION],
                [$class: 'BooleanParameterValue', name: 'DEPLOY_TO_WEBSITE', value: DEPLOY_TO_WEBSITE],
            ]
        )
    }
    if (BUILD_IMAGE) {
        stage('Build CMK IMAGE') {
            build(job: "${FOLDER}/nightly-${JOB_EDITION}/build-cmk-image",
                parameters: [
                    [$class: 'StringParameterValue', name: 'EDITION', value: EDITION],
                    [$class: 'BooleanParameterValue', name: 'PUSH_TO_REGISTRY', value: DEPLOY_TO_WEBSITE],
                ]
            )
        }
    }

    def TEST_STAGES = [:]

    if (RUN_IMAGE_TESTS) {
        TEST_STAGES['Integration Test for Docker Container'] = {
            stage('Integration Test for Docker Container') {
                build(job: "${FOLDER}/nightly-${JOB_EDITION}/test-docker-integration",
                    parameters: [
                        [$class: 'StringParameterValue', name: 'EDITION', value: EDITION]
                    ]
                )
            }
        }
    }

    if (RUN_INTEGRATION_TESTS) {
        TEST_STAGES['Composition Test for Packages'] = {
            stage('Composition Test for Packages') {
                build(job: "${FOLDER}/nightly-${JOB_EDITION}/test-composition",
                    parameters: [
                        [$class: 'StringParameterValue', name: 'DISTROS', value: DISTROS],
                        [$class: 'StringParameterValue', name: 'EDITION', value: EDITION]
                    ]
                )
            }
        }
    }

    parallel(TEST_STAGES)

    if (RUN_INTEGRATION_TESTS) {
        stage('Integration Test for Packages') {
            build(job: "${FOLDER}/nightly-${JOB_EDITION}/test-integration",
                parameters: [
                    [$class: 'StringParameterValue', name: 'DISTROS', value: DISTROS],
                    [$class: 'StringParameterValue', name: 'EDITION', value: EDITION]
                ]
            )
        }
    }
}
