load("@rules_foreign_cc//foreign_cc:defs.bzl", "configure_make")

filegroup(
    name = "all_srcs",
    srcs = glob(
        ["**/*"],
        exclude = ["bazel-*"],
    ),
)

#
# This should be put somewhere toplevel, also avoiding the deprecated
# --define syntax, see
# https://bazel.build/docs/configurable-attributes
#
config_setting(
    name = "omd_libgsf",
    values = {"define": "omd-libgsf=true"},
)

configure_make(
    name = "msitools",
    args = ["-j4"],
    copts = [
        "-Wno-cast-align",
        "-Wno-incompatible-pointer-types-discards-qualifiers",
        "-Wno-sometimes-uninitialized",
        "-Wno-unknown-warning-option",
        "-Wno-unused-but-set-variable",
    ],
    env = {
        "PKG_CONFIG_PATH": ":".join([
            "/usr/lib/pkgconfig",
            "/usr/lib64/pkgconfig",
            "/usr/lib/x86_64-linux-gnu/pkgconfig",
        ]),
    } | select(
        {
            ":omd_libgsf": {
                "GSF_LIBS": "-L$${EXT_BUILD_DEPS}/libgsf/lib -lgsf-1",
                "GSF_CFLAGS": "-I$${EXT_BUILD_DEPS}/libgsf/include/libgsf-1",
            },
            "//conditions:default": {},
        },
    ),
    lib_source = ":all_srcs",
    out_binaries = [
        "msiinfo",
        "msibuild",
    ],
    out_shared_libs = [
        "libmsi.so",
        "libmsi.so.0",
        "libmsi.so.0.0.0",
    ],
    postfix_script = """
        mkdir -p ${INSTALLDIR}/{bin,lib}/
        install -m 755 ${BUILD_TMPDIR}/.libs/msiinfo ${INSTALLDIR}/bin/
        install -m 755 ${BUILD_TMPDIR}/.libs/msibuild ${INSTALLDIR}/bin/
        patchelf --set-rpath "\\$ORIGIN/../lib" ${INSTALLDIR}/bin/msiinfo ${INSTALLDIR}/bin/msibuild
        install -m 755 ${BUILD_TMPDIR}/libmsi/.libs/libmsi.so* ${INSTALLDIR}/lib
    """,
    targets = [
        "-C libmsi",
        "msibuild",
        "msiinfo",
    ],
    visibility = ["//visibility:public"],
    deps = select(
        {
            ":omd_libgsf": ["@libgsf"],
            "//conditions:default": [],
        },
    ),
)
