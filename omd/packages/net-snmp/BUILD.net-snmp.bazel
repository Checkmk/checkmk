load("@@//omd/packages/Python:version.bzl", "PYTHON_MAJOR_DOT_MINOR")
load("@rules_foreign_cc//foreign_cc:defs.bzl", "configure_make")

filegroup(
    name = "all_srcs",
    srcs = glob(
        ["**/*"],
        exclude = ["bazel-*"],
    ),
)

configure_make(
    name = "net-snmp",
    args = ["-j4"],
    build_data = [
        "@@//omd/packages/perl-modules:perl-modules.tar",
        "@@//omd/packages/python3-modules:python3-modules.tar",
    ],
    configure_in_place = True,
    configure_options = [
        "-v",
        "--enable-ipv6",
        "-enable-blumenthal-aes",
        "--disable-agent",
        "--disable-snmptrapd-subagent",
        "--with-defaults",
        "--disable-scripts",
        r'--with-mibdirs="\\$$\\HOME/local/share/snmp/mibs:\\$$\\HOME/share/snmp/mibs:/usr/share/snmp/mibs"',
    ],
    copts = [
        "-I$BUILD_TMPDIR/include",
        "-I${EXT_BUILD_DEPS}/python/include/python%s/" % PYTHON_MAJOR_DOT_MINOR,
        "-I$EXT_BUILD_DEPS/python/lib/",
        "-Wl,--rpath,$ORIGIN_VAR/../lib",
    ],
    env = {
        "PKG_CONFIG_PATH": ":".join([
            "/usr/lib/pkgconfig",
            "/usr/lib64/pkgconfig",
            "/usr/lib/x86_64-linux-gnu/pkgconfig",
        ]),
        "ORIGIN_VAR": "\\\\$$\\$$ORIGIN",
        "RANLIB": "ranlib",
        "LD_LIBRARY_PATH": "$$EXT_BUILD_DEPS/python/lib/",
        "PERL_MODULES_TAR": "$(execpath @@//omd/packages/perl-modules:perl-modules.tar)",
        "PYTHON_MODULES_TAR": "$(execpath @@//omd/packages/python3-modules:python3-modules.tar)",
        "PERL_DIR": "$$(dirname $(execpath @@//omd/packages/perl-modules:perl-modules.tar))/perl-modules",
        "PYTHONPATH": "$$(dirname $(execpath @@//omd/packages/python3-modules:python3-modules.tar))/lib/python%s/site-packages/" % PYTHON_MAJOR_DOT_MINOR,
    },
    lib_source = ":all_srcs",
    out_binaries = [
        "encode_keychange",
        "snmpbulkwalk",
        "snmpdf",
        "snmpgetnext",
        "snmppcap",
        "snmpps",
        "snmpstatus",
        "snmptest",
        "snmptrap",
        "snmpvacm",
        "snmpbulkget",
        "snmpdelta",
        "snmpget",
        "snmpnetstat",
        "snmpping",
        "snmpset",
        "snmptable",
        "snmptranslate",
        "snmpusm",
        "snmpwalk",
    ],
    out_data_dirs = [
        "share/snmp/mibs",
        "share/man/",
        "lib/python%s/site-packages/netsnmp" % PYTHON_MAJOR_DOT_MINOR,
        "lib/perl5/",
    ],
    out_shared_libs = [
        "libnetsnmp.so",
        "libnetsnmp.so.40",
        "libnetsnmp.so.40.1.0",
    ],
    out_static_libs = [
        "libnetsnmp.a",
    ],
    postfix_script = """
        set -x
        tar xf ${PERL_MODULES_TAR} -C $(dirname ${PERL_MODULES_TAR});
        tar xf ${PYTHON_MODULES_TAR} -C $(dirname ${PYTHON_MODULES_TAR});
        mkdir -p lib/python
        cd python && \
            ${EXT_BUILD_DEPS}/python/bin/python3 setup.py install \
                --basedir=.. \
                --root=${INSTALLDIR} \
                --prefix='' \
                --single-version-externally-managed
        cd -
        echo "Build perl"
            make -C perl DESTDIR=${INSTALLDIR}/ \
                INSTALLSITEARCH=/lib/perl5/lib/perl5 \
                INSTALLSITEMAN3DIR=/share/man/man3 \
                INSTALLARCHLIB=/lib/perl5/lib/perl5/x86_64-linux-gnu-thread-multi \
                install

        chmod u+w -R ${INSTALLDIR}
        chmod 644 ${INSTALLDIR}/share/snmp/mibs/*
    """,
    targets = [
        "",
        "-C snmplib DESTDIR='/' installlibs",
        "-C apps DESTDIR='/' installbin",
        "-C man DESTDIR='/' install",
        "-C mibs DESTDIR='/' mibsinstall",
    ],
    visibility = ["//visibility:public"],
    deps = ["@python"],
)

filegroup(
    name = "gen_dir",
    srcs = [":net-snmp"],
    output_group = "gen_dir",
    visibility = ["//visibility:public"],
)
