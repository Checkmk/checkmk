load("@omd_packages//omd/packages/Python:version.bzl", "PYTHON_MAJOR_DOT_MINOR")
load("@rules_foreign_cc//foreign_cc:defs.bzl", "configure_make")

MAKE_PARALLEL = "-j4"

RRDLIB_MAJOR_DOT_MINOR = "8.2.1"

RRDLIB_MAJOR = RRDLIB_MAJOR_DOT_MINOR.split(".")[0]

filegroup(
    name = "all_srcs",
    srcs = glob(
        ["**/*"],
        exclude = ["bazel-*"],
    ),
)

configure_make(
    name = "rrdtool",
    args = ["%s" % MAKE_PARALLEL],
    build_data = [
        "@omd_packages//omd/packages/perl-modules:perl-modules.tar",
        "@omd_packages//omd/packages/python3-modules:python3-modules.tar",
    ],
    configure_in_place = True,
    configure_options = [
        "--verbose",
        "--disable-ruby",
        "--disable-libwrap",
        "--enable-perl-site-install",
        "--disable-tcl",
        "--disable-lua",
        "--disable-rrdcgi",
        "--disable-libdbi",
        "--with-systemdsystemunitdir=no",
        "--with-perl-options='LIB=$${EXT_BUILD_DEPS}/lib/perl5/lib/perl5'",
        "CFLAGS='-Dredacted=\"redacted\"'",
        # Give up on pkg-config.  It's broken after updating foreign_cc
        # from 0.9.  We should switch to an entirely bazel-native solution
        # for this dependency soonish anyway.
        "PKGCONFIG='no'",
        "CPPFLAGS='" + " ".join([
            "-I/usr/include/blkid",
            "-I/usr/include/cairo",
            "-I/usr/include/freetype2",
            "-I/usr/include/fribidi",
            "-I/usr/include/glib-2.0",
            "-I/usr/include/harfbuzz",
            "-I/usr/include/libmount",
            "-I/usr/include/libpng16",
            "-I/usr/include/libxml2",
            "-I/usr/include/pango-1.0",
            "-I/usr/include/pixman-1",
            "-I/usr/include/uuid",
            "-I/usr/lib64/glib-2.0",
            "-I/usr/lib64/glib-2.0/include",
            "-I/usr/lib/x86_64-linux-gnu/glib-2.0/include",
        ]) + "'",
    ],
    copts = [
        "-I$BUILD_TMPDIR/include",
        "-Wno-stringop-overflow",
    ],
    env = {
        "ORIGIN_VAR": "\\\\$$\\$$ORIGIN",
        "RANLIB": "ranlib",
        "LD_LIBRARY_PATH": "$$EXT_BUILD_DEPS/python/lib/",
        "PERL_DIR": "$$EXT_BUILD_DEPS/omd/packages/perl-modules/perl-modules",
        "CFLAGS": "'-Dredacted=\"redacted\"' -I$$EXT_BUILD_DEPS/python/include/python%s" % PYTHON_MAJOR_DOT_MINOR,
        "PYTHONPATH": "$$(cd $$(dirname $(execpath @omd_packages//omd/packages/python3-modules:python3-modules.tar)); tar xf python3-modules.tar;)$$(dirname $(execpath @omd_packages//omd/packages/python3-modules:python3-modules.tar))/python3-modules/lib/python%s/site-packages/" % PYTHON_MAJOR_DOT_MINOR,
        "PYTHON_3_VERSION": "%s" % PYTHON_MAJOR_DOT_MINOR,
        "PATH": "$$EXT_BUILD_DEPS/python/bin/:$$PATH",
        "top_builddir": ".",
    },
    install_prefix = "$INSTALLDIR",
    lib_source = ":all_srcs",
    linkopts = [
        "-lcairo",
        "-lglib-2.0",
        "-lgobject-2.0",
        "-lpango-1.0",
        "-lpangocairo-1.0",
        "-lpng",
    ],
    out_binaries = [
        "rrdcached",
        "rrdcreate",
        "rrdinfo",
        "rrdtool",
        "rrdupdate",
    ],
    out_data_dirs = [
        "share",
        "tar",
        "lib/perl5/lib/perl5/",
        "lib/python%s/site-packages/" % PYTHON_MAJOR_DOT_MINOR,
        "lib/pkgconfig",
    ],
    out_shared_libs = [
        "librrd.so",
        "librrd.so.%s" % RRDLIB_MAJOR,
        "librrd.so.%s" % RRDLIB_MAJOR_DOT_MINOR,
    ],
    out_static_libs = [
        "librrd.a",
    ],
    postfix_script = """
        # The install step doesn't put the perl bindings where we
        # expect them.  So let's copy them here.
        LPLP="lib/perl5/lib/perl5"
        cp -R ${INSTALLDIR}${EXT_BUILD_DEPS}/$LPLP/* ${INSTALLDIR}/$LPLP
        chmod u+w -R ${INSTALLDIR}/../
        patchelf --set-rpath "\\$ORIGIN/../lib" \
                ${INSTALLDIR}/bin/rrdcached \
                ${INSTALLDIR}/bin/rrdcreate \
                ${INSTALLDIR}/bin/rrdinfo \
                ${INSTALLDIR}/bin/rrdtool \
                ${INSTALLDIR}/bin/rrdupdate
        tar -C ${INSTALLDIR}/../ \
            --exclude="rrdtool/home" \
            --exclude="rrdtool/usr" \
            --exclude="rrdtool/tar" \
            -cf ${INSTALLDIR}/tar/rrdtool.tar \
            rrdtool
    """,
    targets = [
        "-C po all",
        "-C src all",
        "-C tests all",
        "-C etc all",
        # Explicitly require perl and python bindings to make sure
        # we fail if they are not built.
        "-C bindings perl-piped",
        "-C bindings perl-shared",
        "-C bindings python",
        "-C po install",
        "-C src install",
        "-C tests install",
        "-C etc install",
        # DESTDIR for Perl, since the prefix is not honored
        "DESTDIR=$INSTALLDIR -C bindings install",
        # DESTDIR for Python, since INSTALLDIR is in prefix
        "DESTDIR=/ -C bindings install",
    ],
    visibility = ["//visibility:public"],
    deps = ["@python"],
)
